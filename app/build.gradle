apply plugin: 'com.android.application'
apply plugin: 'com.hiya.jacoco-android'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.sherter.google-java-format'

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.hiya:jacoco-android:0.2'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3'
        classpath 'gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.9'
    }
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}

android {
    compileSdkVersion 30
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "edu.luc.etl.cs313.android.clickcounter"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
    sourceSets {
        final String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

dependencies {
    // several duplications on account of shared testcase superclass
    testImplementation 'junit:junit:4.13'
    testImplementation 'com.android.support.test:rules:1.0.2'
    testImplementation 'org.robolectric:robolectric:4.4'
    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}
